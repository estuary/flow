{
  description = "Hermetic base system environment for Flow";

  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs/nixos-25.05";
    flake-utils.url = "github:numtide/flake-utils";
  };

  outputs = { self, nixpkgs, flake-utils }:
    flake-utils.lib.eachDefaultSystem (system:
      let
        pkgs = nixpkgs.legacyPackages.${system};
        buildInputs = with pkgs; [
          bash
          bazelisk
          bzip2
          clang
          coreutils
          gawk
          gcc
          gnugrep
          gnused
          jemalloc
          libclang
          libcxx
          liburing
          lz4
          openssl
          pkg-config
          rocksdb
          snappy
          supabase-cli
          zlib
          zstd
        ];
      in
      {
        devShells.default = pkgs.mkShell {
          name = "flow-dev";

          buildInputs = buildInputs;

          shellHook = ''
            # Override PATH to only use nix-provided tools
            export PATH="${pkgs.lib.makeBinPath buildInputs}"

            # C/C++ compiler for Bazel
            export CC="${pkgs.clang}/bin/clang"
            export CXX="${pkgs.clang}/bin/clang++"

            # C++ standard library headers and libraries
            export CPLUS_INCLUDE_PATH="${pkgs.libcxx.dev}/include/c++/v1"
            export NIX_LDFLAGS="-L${pkgs.libcxx}/lib -lc++ -lc++abi"
            export LIBRARY_PATH="${pkgs.libcxx}/lib"
            export LIBCXX_LIB_DIR="${pkgs.libcxx}/lib"

            # libclang for bindgen (Rust FFI bindings generator)
            export LIBCLANG_PATH="${pkgs.libclang.lib}/lib"

            # RocksDB paths for system library
            export ROCKSDB_INCLUDE_DIR="${pkgs.rocksdb}/include"
            export ROCKSDB_LIB_DIR="${pkgs.rocksdb}/lib"
            export ROCKSDB_STATIC="1"

            export OPENSSL_INCLUDE_DIR="${pkgs.openssl.dev}/include"
            export OPENSSL_LIB_DIR="${pkgs.openssl.out}/lib"

            export JEMALLOC_OVERRIDE="${pkgs.jemalloc}/lib/libjemalloc.a"

            # Generate .bazelrc.local with libc++ paths from Nix
            cat > .bazelrc.local <<EOF
# Auto-generated by flake.nix - DO NOT EDIT
# This file contains Nix store paths for libc++ static libraries

# Link against libc++ for Rust builds
build --@rules_rust//rust/settings:extra_rustc_flag=-Clink-arg=${pkgs.libcxx}/lib/libc++.a
build --@rules_rust//rust/settings:extra_rustc_flag=-Clink-arg=${pkgs.libcxx}/lib/libc++abi.a
build --@rules_rust//rust/settings:extra_rustc_flag=-Clink-arg=-lgcc
build --@rules_rust//rust/settings:extra_exec_rustc_flag=-Clink-arg=${pkgs.libcxx}/lib/libc++.a
build --@rules_rust//rust/settings:extra_exec_rustc_flag=-Clink-arg=${pkgs.libcxx}/lib/libc++abi.a
build --@rules_rust//rust/settings:extra_exec_rustc_flag=-Clink-arg=-lgcc
EOF

            echo ""
            echo "Generated .bazelrc.local with libc++ paths"
          '';
        };
      }
    );
}
